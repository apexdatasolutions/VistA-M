GMPLSLRP ;ISP/TC - Problem Selection List Reports ;08/22/17  06:38
 ;;2.0;Problem List;**49**;Aug 25, 1994;Build 43
 ;
 ; External References:
 ;   ICR  2056   $$GET1^DIQ
 ;   ICR  10026  ^DIR
 ;   ICR  10060  ^VA(200,"B"
 ;   ICR  10104  $$REPEAT^XLFSTR
 ;
GETUSRLT(GMPLULST,GMPLELST) ; Retrieve pre-existing user problem selection lists
 N GMPLUSR,GMPLDUZ,GMPLLNME,GMPLUSNM,GMPLST
 S (GMPLUSR,GMPLDUZ)=""
 F  S GMPLUSR=$O(^VA(200,"B",GMPLUSR)) Q:GMPLUSR=""  D
 . F  S GMPLDUZ=$O(^VA(200,"B",GMPLUSR,GMPLDUZ)) Q:GMPLDUZ=""  D
 . . S GMPLUSNM=$$GET1^DIQ(200,GMPLDUZ,.01) Q:'$L(GMPLUSNM)
 . . S GMPLLNME=$$GET1^DIQ(200,GMPLDUZ,125.1),GMPLST=$$GET1^DIQ(200,GMPLDUZ,125.1,"I")
 . . I '$L(GMPLLNME),GMPLST S GMPLELST(GMPLUSNM,GMPLST)=GMPLDUZ Q
 . . E  Q:'$L(GMPLLNME)
 . . S GMPLULST(GMPLUSNM,GMPLLNME)=""
 Q
 ;
GETCLNLT(GMPLCLST) ; Retrieve pre-existing clinic problem selection lists
 N GMPLCLIN,GMPLST,GMPLDA
 S (GMPLDA,GMPLST)=""
 F  S GMPLST=$O(^GMPL(125,"B",GMPLST)) Q:GMPLST=""  D
 . F  S GMPLDA=$O(^GMPL(125,"B",GMPLST,GMPLDA)) Q:GMPLDA=""  D
 . . S GMPLCLIN=$$GET1^DIQ(125,GMPLDA,.03) Q:GMPLCLIN=""
 . . S GMPLCLST(GMPLCLIN,GMPLST)=""
 Q
 ;
PAGE(GMPLNUM,GMPLTITL) ; Print header and raise page number
 Q:'$G(GMPLNUM)
 I GMPLNUM'=1,$E(IOST,1,2)="C-" D  Q:'GMPLNUM
 . N DIR,DTOUT,DIRUT,DUOUT,X,Y
 . S DIR(0)="E" D ^DIR
 . I $D(DTOUT)!($D(DUOUT)) S GMPLNUM=0
 W @IOF
 W GMPLTITL
 W ?70,"Page: ",GMPLNUM
 W !,$$REPEAT^XLFSTR("-",78)
 S GMPLNUM=GMPLNUM+1
 Q
