PXRMP35I ;SLC/PKR - Inits for PXRM*2.0*35. ;02/03/2016
 ;;2.0;CLINICAL REMINDERS;**35**;Feb 04, 2005;Build 206
 Q
 ;==========================================
PRE ;Pre-init
 ;Disable options and protocols
 N P47I
 S P47I=$$PATCH^XPDUTL("PXRM*2.0*47")
 I 'P47I D
 . D OPTION^PXRMUTIL("DISABLE")
 . D PROTOCOL^PXRMUTIL("DISABLE")
 I P47I D
 . D OPTIONS^PXRMUTIL("DISABLE","INSTALL OF PXRM*2.0*35")
 . D PROTCOLS^PXRMUTIL("DISABLE","INSTALL OF PXRM*2.0*35")
 D DELEXE^PXRMEXSI("EXARRAY","PXRMP35E")
 Q
 ;
 ;==========================================
POST ;Post-init
 ;Enable options and protocols
 N P47I
 S P47I=$$PATCH^XPDUTL("PXRM*2.0*47")
 I 'P47I D
 . D OPTION^PXRMUTIL("ENABLE")
 . D PROTOCOL^PXRMUTIL("ENABLE")
 I P47I D
 . D OPTIONS^PXRMUTIL("ENABLE","INSTALL OF PXRM*2.0*35")
 . D PROTCOLS^PXRMUTIL("ENABLE","INSTALL OF PXRM*2.0*35")
 D SETPVER^PXRMUTIL("2.0P35")
 ;Install Exchange File entries.
 D SMEXINS^PXRMEXSI("EXARRAY","PXRMP35E")
 D SENDIM^PXRMP35I
 Q
 ;
 ;==========================================
SENDIM ;Send install message.
 N FROM,NODE,PARAM,SYSTEM,SUBJECT,TO,VALUE
 S NODE="PXRM*2.0*35"
 K ^TMP(NODE,$J)
 ;DBIA #1131 for ^XMB("NETNAME")
 S FROM="PXRM*2.0*35 Install@"_^XMB("NETNAME")
 ;DBIA #2541
 S SYSTEM=$$KSP^XUPARAM("WHERE")
 I $$PROD^XUPROD(1) S TO("G.CLINICAL REMINDERS SUPPORT@domain.ext")=""
 E  D
 . N MGIEN,MGROUP
 . S MGIEN=$G(^PXRM(800,1,"MGFE"))
 . S MGROUP=$S(MGIEN'="":"G."_$$GET1^DIQ(3.8,MGIEN,.01),1:DUZ)
 . S TO(MGROUP)=""
 S SUBJECT="Install of PXRM*2.0*35"
 S ^TMP(NODE,$J,1,0)="PXRM*2.0*35 has been installed."
 S ^TMP(NODE,$J,2,0)="System is "_SYSTEM
 D SEND^PXRMMSG(NODE,SUBJECT,.TO,FROM)
 Q
 ;
